archetype (adl_version=1.4; uid=f0cc65ca-6e99-4992-9851-5d95202003df)
	openEHR-EHR-CLUSTER.lund_browder_chart.v0

concept
	[at0000]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"2020-07-29">
		["name"] = <"Erik Sundvall">
		["organisation"] = <"Region Östergötland & Linköping University">
		["email"] = <"erik.sundvall@regionostergotland.se & erik.sundvall@liu.se">
	>
	lifecycle_state = <"unmanaged">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording percentage of total body area affected by burns of different depths.">
			keywords = <"burns, burn assessment, Lund-Browder, skin area", ...>
			use = <"Use in 'Examination findings' slots of other archetypes, for example CLUSTER.exam, CLUSTER.exam-skin or CLUSTER.exam_burn

Systems should preferably not store anything (e.g. not store 0%) for body part sections and burn depth levels within them that are not affected by burns for the patient.
">
		>
	>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 4.0 International License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/4.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Hettiaratchy S, Papini R. Initial management of a major burn: II--assessment and resuscitation. BMJ. 2004;329(7457):101-103. doi:10.1136/bmj.329.7457.101 Available from: https://www.ncbi.nlm.nih.gov/pmc/articles/PMC449823/

Lund-Browder Charts
https://www.remm.nlm.gov/burns.htm & https://www.remm.nlm.gov/LundBrowder.pdf
https://www.rch.org.au/burns/clinical_information/#lund-and-browder-chart
https://en.wikipedia.org/wiki/Lund_and_Browder_chart">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"f7461010b51fb39adf1408155a1e9314">
		["build_uid"] = <"aa8b6609-e455-3555-8ac9-847b2a893045">
		["ip_acknowledgements"] = <"This artefact includes content from SNOMED Clinical Terms® (SNOMED CT®) which is copyrighted material of the International Health Terminology Standards Development Organisation (IHTSDO). Where an implementation of this artefact makes use of SNOMED CT content, the implementer must have the appropriate SNOMED CT Affiliate license - for more information contact http://www.snomed.org/snomed-ct/get-snomedct or info@snomed.org.">
	>

definition
	CLUSTER[at0000] matches {    -- Relative body surface area assessment
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0033] occurrences matches {0..1} matches {    -- Estmiation method
				value matches {
					DV_CODED_TEXT matches {
						defining_code matches {
							[local::
							at0034,    -- Lund-Browder Chart
							at0035,    -- Modified Rule of nines for pediatric use
							at0036,    -- Rule of nines (unmodified)
							at0037,    -- Palm rule
							at0042]    -- Image analysis
						}
					}
				}
			}
			ELEMENT[at0026] occurrences matches {0..1} matches {    -- Age category used for Lund-Browder
				value matches {
					0|[local::at0027],
					1|[local::at0028],
					5|[local::at0029],
					10|[local::at0030],
					15|[local::at0031]
				}
			}
			ELEMENT[at0041] occurrences matches {0..1} matches {    -- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0001] occurrences matches {0..1} matches {    -- Partial area
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0003] occurrences matches {0..1} matches {    -- Body part
						value matches {
							DV_CODED_TEXT matches {*}
						}
					}
					ELEMENT[at0002] occurrences matches {0..1} matches {    -- Body part area of total body
						value matches {
							DV_PROPORTION matches {
								type matches {2}
							}
						}
					}
					CLUSTER[at0004] occurrences matches {0..1} matches {    -- Burns per depth
						items cardinality matches {1..*; unordered} matches {
							ELEMENT[at0005] occurrences matches {0..1} matches {    -- Depth
								value matches {
									DV_CODED_TEXT matches {
										defining_code matches {
											[local::
											at0010,    -- Partial thickness burn
											at0011,    -- Deep partial thickness burn
											at0012,    -- Full thickness burn
											at0013,    -- Deep full thickness burn
											at0032]    -- Epidermal burn
										}
									}
								}
							}
							ELEMENT[at0006] occurrences matches {0..1} matches {    -- Burnt area, precentage of full body
								value matches {
									DV_PROPORTION matches {
										type matches {2}
									}
								}
							}
							ELEMENT[at0007] occurrences matches {0..1} matches {    -- Coded expression
								value matches {
									DV_CODED_TEXT matches {*}
								}
							}
						}
					}
					ELEMENT[at0008] occurrences matches {0..1} matches {    -- Sum of burnt area on this part
						value matches {
							DV_PROPORTION matches {
								type matches {2}
							}
						}
					}
					allow_archetype CLUSTER[at0038] occurrences matches {0..*} matches {    -- Extension per part
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
			CLUSTER[at0014] occurrences matches {0..1} matches {    -- Sum of areas
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0025] occurrences matches {0..1} matches {    -- TBSA%, Sum of affected/burnt area of whole body
						value matches {
							DV_PROPORTION matches {
								type matches {2}
							}
						}
					}
					CLUSTER[at0017] occurrences matches {0..1} matches {    -- Burns per depth category
						items cardinality matches {1..*; unordered} matches {
							use_node ELEMENT occurrences matches {0..1} /items[at0001]/items[at0004]/items[at0005]
							ELEMENT[at0023] occurrences matches {0..1} matches {    -- Total burnt area of this depth category
								value matches {
									DV_PROPORTION matches {*}
								}
							}
							ELEMENT[at0024] occurrences matches {0..1} matches {    -- Coded expression
								value matches {
									DV_CODED_TEXT matches {*}
								}
							}
						}
					}
					allow_archetype CLUSTER[at0039] occurrences matches {0..*} matches {    -- Sum extension
						include
							archetype_id/value matches {/.*/}
					}
				}
			}
			allow_archetype CLUSTER[at0043] occurrences matches {0..*} matches {    -- Extension
				include
					archetype_id/value matches {/.*/}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Relative body surface area assessment">
					description = <"Burns area assessment using methods like Lund browder chart, Rule of nines, ">
				>
				["at0001"] = <
					text = <"Partial area">
					description = <"To be repeated and configured in template, if the predefined body parts/areas in available templates or archetype specializations do not fit the use case.">
					comment = <"Example: In some use cases upper arm and lower arm are recorded separately and in some other use cases recorded together as arm. 

Remember to remove (0..0) old overlapping parts if new ones are introduced, to avoid possibility of double registration.">
				>
				["at0002"] = <
					text = <"Body part area of total body">
					description = <"The age-category-dependent size the Lund-Browder chart assigns to this body part. The 'Total burns on this part' summed up of different kinds of burns on this body part should never exceed this number.
    
When 'Age category' has been selected, this number could be automatically filled by the system.">
				>
				["at0003"] = <
					text = <"Body part">
					description = <"Preferably coded with terminology like Snomed CT.">
				>
				["at0004"] = <
					text = <"Burns per depth">
					description = <"Repeat once for each depth category used in template.">
				>
				["at0005"] = <
					text = <"Depth">
					description = <"The burn depth category used for the estimation, for example partial thickness (second degree) burn">
					comment = <"Some applications of e.g. the Lund-Browder chart only discern Partial v.s. Full thickness burns, but others use more granular levels.

Corresponding SNOMED CT codes are available.">
				>
				["at0006"] = <
					text = <"Burnt area, precentage of full body">
					description = <"">
				>
				["at0007"] = <
					text = <"Coded expression">
					description = <"Optional terminology codes or expressions representing the combined burn site and depth. 

Can be used in templates where both 'Generic part' and 'Burns per depth' have been repeated and configured/locked to specific values in a table-like structure.">
					comment = <"T">
				>
				["at0008"] = <
					text = <"Sum of burnt area on this part">
					description = <"">
				>
				["at0010"] = <
					text = <"Partial thickness burn">
					description = <"Second degree burn / Mid dermal burn">
				>
				["at0011"] = <
					text = <"Deep partial thickness burn">
					description = <"Deep second degree burn / Deep dermal burn">
				>
				["at0012"] = <
					text = <"Full thickness burn">
					description = <"Third degree burn">
				>
				["at0013"] = <
					text = <"Deep full thickness burn">
					description = <"Fourth degree burn">
				>
				["at0014"] = <
					text = <"Sum of areas">
					description = <"">
				>
				["at0017"] = <
					text = <"Burns per depth category">
					description = <"">
				>
				["at0023"] = <
					text = <"Total burnt area of this depth category">
					description = <"Total burnt area of this depth category, precentage of full body">
				>
				["at0024"] = <
					text = <"Coded expression">
					description = <"">
				>
				["at0025"] = <
					text = <"TBSA%, Sum of affected/burnt area of whole body">
					description = <"Sum of burnt area of whole body">
					comment = <"Summing up all 'Sum of burnt area on this part' for all body parts should equal summing up all 'Total burnt area for this depth'">
				>
				["at0026"] = <
					text = <"Age category used for Lund-Browder">
					description = <"Age category used for area estimation in Lund-Browder chart">
				>
				["at0027"] = <
					text = <"0-1 years">
					description = <"Scale for infants and children less than 1 year old">
				>
				["at0028"] = <
					text = <"1-4 years">
					description = <"Scale for children 1-4 years old">
				>
				["at0029"] = <
					text = <"5-9 years">
					description = <"Scale for children 5-9 years old">
				>
				["at0030"] = <
					text = <"10-15 years">
					description = <"Scale for children 10-15 years old">
				>
				["at0031"] = <
					text = <"Adult, 15+ years">
					description = <"Scale for adults and adolescents 15 years or older">
				>
				["at0032"] = <
					text = <"Epidermal burn">
					description = <"First degree burn, Superficial epidermal burn (For example sunburn) Not included in TBSA%">
				>
				["at0033"] = <
					text = <"Estmiation method">
					description = <"">
				>
				["at0034"] = <
					text = <"Lund-Browder Chart">
					description = <"">
				>
				["at0035"] = <
					text = <"Modified Rule of nines for pediatric use">
					description = <"">
				>
				["at0036"] = <
					text = <"Rule of nines (unmodified)">
					description = <"Not accurate for pediatric use. DIvides body into fractions of multiples of 9%">
				>
				["at0037"] = <
					text = <"Palm rule">
					description = <"Based on that that palm of the patient's hand including fingers (in rested state) is approximately 1%">
				>
				["at0038"] = <
					text = <"Extension per part">
					description = <"">
				>
				["at0039"] = <
					text = <"Sum extension">
					description = <"">
				>
				["at0041"] = <
					text = <"Comment">
					description = <"">
				>
				["at0042"] = <
					text = <"Image analysis">
					description = <"Algorithm (software) based analysis of data obtained by imaging methods">
				>
				["at0043"] = <
					text = <"Extension">
					description = <"">
				>
				["at0044"] = <
					text = <"ELEMENT">
					description = <"">
				>
			>
		>
	>
