archetype (adl_version=1.4; uid=539560a6-eda7-41b6-9a3d-386fe0c2815c)
	openEHR-EHR-CLUSTER.communication.v0

concept
	[at0000]	-- Communication
language
	original_language = <[ISO_639-1::en]>
	translations = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			author = <
				["name"] = <"John Tore Valand">
				["organisation"] = <"Helse Bergen HF, Norway">
				["email"] = <"john.tore.valand@helse-bergen.no">
			>
		>
	>
description
	original_author = <
		["name"] = <"Heather Leslie">
		["organisation"] = <"Atomica Informatics, Australia">
		["email"] = <"heather.leslie@atomicainformatics.com">
		["date"] = <"2018-03-26">
	>
	details = <
		["nb"] = <
			language = <[ISO_639-1::nb]>
			purpose = <"For å registrere metoden(e) et individ bruker for å formidle eller motta informasjon.">
			use = <"Brukes for å registrere metoden(e) et individ bruker for å formidle eller motta informasjon.">
			keywords = <"språk", "tolk">
			misuse = <"">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To record the method/s used by an individual to impart or receive information.">
			use = <"Use to record the method/s used by an individual to impart or receive information.">
			misuse = <"">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <"Silje Ljosland Bakke, Nasjonal IKT HF, Norway", "John Tore Valand, Helse Bergen HF, Norway", "Vebjørn Arntzen, Oslo University Hospital, Norway">
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["current_contact"] = <"Heather Leslie, Atomica Informatics, Australia<heather.leslie@atomicainformatics.com>">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"9AD62CA207C81E36D5D2370974350871">
		["build_uid"] = <"cc016be4-1339-450d-b84c-4265fa45af90">
		["revision"] = <"0.0.1-alpha">
	>

definition
	CLUSTER[at0000] matches {	-- Communication
		items cardinality matches {1..*; unordered} matches {
			ELEMENT[at0001] occurrences matches {0..1} matches {	-- Description
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0002] occurrences matches {0..*} matches {	-- Translation requirements
				items cardinality matches {2..*; unordered} matches {
					ELEMENT[at0003] matches {	-- Language name
						value matches {
							DV_TEXT matches {*}
						}
					}
					ELEMENT[at0004] matches {	-- Translator required
						value matches {
							DV_BOOLEAN matches {
								value matches {True, False}
							}
						}
					}
				}
			}
			allow_archetype CLUSTER[at0005] occurrences matches {0..*} matches {	-- Preferred communication
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.language(-[a-zA-Z0-9_]+)*\.v0/}
			}
			allow_archetype CLUSTER[at0006] occurrences matches {0..*} matches {	-- Other communication
				include
					archetype_id/value matches {/openEHR-EHR-CLUSTER\.language(-[a-zA-Z0-9_]+)*\.v0/}
			}
			ELEMENT[at0007] occurrences matches {0..1} matches {	-- Comment
				value matches {
					DV_TEXT matches {*}
				}
			}
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Communication">
					description = <"The capability and means of imparting or exchanging of information by an individual.">
				>
				["at0001"] = <
					text = <"Description">
					description = <"Narrative description about the overall communication capability of an individual.">
				>
				["at0002"] = <
					text = <"Translation requirements">
					description = <"Details about translator requirements for an identified language.">
					comment = <"In clinical systems it is common to record whether a interpreter is required as a simple, standalone boolean data point - the language for translation required is implied, rather than specified. However if this single boolean is exchanged to a location where the assumed language is different, the recorded boolean may be incorrect. In order for this data to be correctly exchanged, both the language and requirement for translation needs to be exchanged as a pair. Each language spoken by an individual will require a separate assessment of whether a translator is required.">
				>
				["at0003"] = <
					text = <"Language name">
					description = <"Identification of the language, by name.">
				>
				["at0004"] = <
					text = <"Translator required">
					description = <"Indicator that the services of a translator are required for the identified language.">
				>
				["at0005"] = <
					text = <"Preferred communication">
					description = <"The preferred language and method of communication for an individual.">
					comment = <"More than one instances of the CLUSTER.language may be required here if the individual uses a variety modes of communication, for example speaking in one language and signing in another.">
				>
				["at0006"] = <
					text = <"Other communication">
					description = <"Other languages, and/or methods of communication for an individual.">
				>
				["at0007"] = <
					text = <"Comment">
					description = <"Additional narrative about the communication capability of an individual not captured in other fields.">
				>
			>
		>
		["nb"] = <
			items = <
				["at0000"] = <
					text = <"Kommunikasjon">
					description = <"Individets evne og midler for å formidle eller utveksle informasjon.">
				>
				["at0001"] = <
					text = <"Beskrivelse">
					description = <"En fritekstbeskrivelse om individets overordnede evne til kommunikasjon.">
				>
				["at0002"] = <
					text = <"Tolkebehov">
					description = <"Detaljer om tolkebehovet for ett identifisert språk.">
					comment = <"I kliniske system er det vanlig å registrere om det er behov for tolk med en registrering i et enkelt boolsk dataelement. Oversettelsesspråket er underforstått og spesifiseres ikke. Utveksles denne verdien til en lokalisering hvor oversettelsesspråket er et annet kan den registrerte boolske verdien være feil. For å sikre en riktig utveksling av dette dataelementet, må både språket og behov for tolk utveksles sammen. Hvert språk som individet snakker vil trenge en en egen vurdering av om det er et tolkebehov.">
				>
				["at0003"] = <
					text = <"Navn på språk">
					description = <"Navnet på språket.">
				>
				["at0004"] = <
					text = <"Trenger tolk">
					description = <"Indikererer at det er et tolkebehov for det identifiserte språket.">
				>
				["at0005"] = <
					text = <"Foretrukket kommunikasjon">
					description = <"Individets foretrukne språk og kommunikasjonsmetode.">
					comment = <"Det kan være behov for flere instanser av CLUSTER.language om individet benytter flere kommunikasjonsmetoder. For eksempel: snakker ett språk og benytter et annet for tegnspråk.">
				>
				["at0006"] = <
					text = <"Annen kommunikasjon">
					description = <"Individets andre språk og kommunikasjonsmetoder.">
				>
				["at0007"] = <
					text = <"Kommentar">
					description = <"Ytterligere fritekstbeskrivelse om individets evne til kommunikasjon som ikke er registrert i andre felt.">
				>
			>
		>
	>
