archetype (adl_version=1.4; uid=456a2b23-9a06-4728-8dbc-b6d3d5e3b57e)
	openEHR-EHR-OBSERVATION.bishop_score.v0

concept
	[at0000]	-- Bishop score
language
	original_language = <[ISO_639-1::en]>
description
	original_author = <
		["name"] = <"Marit Alice Venheim">
		["organisation"] = <"Helse Vest IKT AS">
		["email"] = <"marit.alice.venheim@helse-vest-ikt.no">
		["date"] = <"2019-02-05">
	>
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"To rate the readiness of the cervix, for induction of labour, by standard digital vaginal assessment.">
			use = <"To rate the readiness of the cervix for induction of labour in pregnent women.
The Bishop score is the most commonly used method to rate the readiness of the cervix for induction of labour. ">
			keywords = <"Bishop score, cervix score, the uterine cervix (the neck of the womb), induction of labour, cervical readiness (ripeness), standard digital vaginal assessment", ...>
			misuse = <"Not used for ordinary standard digital vaginal assessment during active labour or pre-labour. Use the 	
CLUSTER.palpation_of_cervix for this purpose.">
			copyright = <"© openEHR Foundation">
		>
	>
	lifecycle_state = <"in_development">
	other_contributors = <>
	other_details = <
		["licence"] = <"This work is licensed under the Creative Commons Attribution-ShareAlike 3.0 License. To view a copy of this license, visit http://creativecommons.org/licenses/by-sa/3.0/.">
		["custodian_organisation"] = <"openEHR Foundation">
		["references"] = <"Bishop score WikipediA:  https://en.wikipedia.org/wiki/Bishop_score 
Original Bishop score with modifications, https://europepmc.org/abstract/med/27918160
Ezebialu, I. U., et al. (2015). \"Methods for assessing pre‐induction cervical ripening.\" Cochrane Database of Systematic Reviews(6). https://www.cochranelibrary.com/cdsr/doi/10.1002/14651858.CD010762.pub2/full?highlightAbstract=withdrawn%7Cscore%7Cbishop%7Cscor">
		["original_namespace"] = <"org.openehr">
		["original_publisher"] = <"openEHR Foundation">
		["custodian_namespace"] = <"org.openehr">
		["MD5-CAM-1.0.1"] = <"51FCBF11C2E05CD1B34D00CA2FAE4419">
		["build_uid"] = <"d58d8f50-fe66-4657-9ca9-f2f2e5691303">
		["revision"] = <"0.0.1-alpha">
	>

definition
	OBSERVATION[at0000] matches {    -- Bishop score
		data matches {
			HISTORY[at0001] matches {    -- History
				events cardinality matches {0..*; unordered} matches {
					POINT_EVENT[at0002] occurrences matches {0..*} matches {    -- Point in time
						data matches {
							ITEM_TREE[at0003] matches {    -- ItemTree
								items cardinality matches {0..*; unordered} matches {
									ELEMENT[at0023] occurrences matches {0..1} matches {    -- Cervical position
										value matches {
											0|[local::at0024],
											1|[local::at0025],
											2|[local::at0026]
										}
									}
									ELEMENT[at0019] occurrences matches {0..1} matches {    -- Cervical consistency
										value matches {
											0|[local::at0020],
											1|[local::at0021],
											2|[local::at0022]
										}
									}
									ELEMENT[at0014] occurrences matches {0..1} matches {    -- Cervical effacement
										value matches {
											0|[local::at0015],
											1|[local::at0016],
											2|[local::at0017],
											3|[local::at0018]
										}
									}
									ELEMENT[at0009] occurrences matches {0..1} matches {    -- Cervical dilation
										value matches {
											0|[local::at0010],
											1|[local::at0011],
											2|[local::at0012],
											3|[local::at0013]
										}
									}
									ELEMENT[at0004] occurrences matches {0..1} matches {    -- Fetal station
										value matches {
											0|[local::at0005],
											1|[local::at0006],
											2|[local::at0007],
											3|[local::at0008]
										}
									}
									ELEMENT[at0027] occurrences matches {0..1} matches {    -- Total score
										value matches {
											DV_COUNT matches {
												magnitude matches {|0..10|}
											}
										}
									}
									ELEMENT[at0030] occurrences matches {0..1} matches {    -- Comment
										value matches {
											DV_TEXT matches {*}
										}
									}
								}
							}
						}
					}
				}
			}
		}
		protocol matches {
			ITEM_TREE[at0028] matches {*}    -- ItemTree
		}
	}


ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Bishop score">
					description = <"Bishop score, known as cervix score, is a pre-labor scoring system. 
">
				>
				["at0001"] = <
					text = <"History">
					description = <"*">
				>
				["at0002"] = <
					text = <"Point in time">
					description = <"A specific date and/or time which may be explicitly defined in a template or at run-time. ">
				>
				["at0003"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0004"] = <
					text = <"Fetal station">
					description = <"Fetal station describes the position of the fetus's head in relation to the distance from the ischial spines.
Negative numbers indicate that the head is further inside than the ischial spines and positive numbers show that the head is below the level of the ischial spines.">
				>
				["at0005"] = <
					text = <"- 3">
					description = <"Fetal station is 3cm above the level of the ischial spines.">
				>
				["at0006"] = <
					text = <"- 2">
					description = <"Fetal station is 2cm above the level of the ischial spines.">
				>
				["at0007"] = <
					text = <"-1, 0">
					description = <"Fetal station is 1cm above the level of the ischial spines or leveled with the ischial spines; 0cm.">
				>
				["at0008"] = <
					text = <"+1, +2">
					description = <"Fetal station is 1 or 2cm below the level of the ischial spines.">
				>
				["at0009"] = <
					text = <"Cervical dilation">
					description = <"Diameter of cervical os.">
				>
				["at0010"] = <
					text = <"Closed">
					description = <"The cervical os is closed.">
				>
				["at0011"] = <
					text = <"1-2 cm">
					description = <"The cervical os is 1-2 cm dilated.">
				>
				["at0012"] = <
					text = <"3-4 cm">
					description = <"The cervical os is 3-4 cm dilated.">
				>
				["at0013"] = <
					text = <"5+ cm">
					description = <"The cervical os is 5+ cm dilated.">
				>
				["at0014"] = <
					text = <"Cervical effacement">
					description = <"Effacement translates to how 'thin' the cervix is. The cervix is normally approximately three centimetres long, as it prepares for labour and labour continues the cervix will efface till it is 'fully effaced' (paper-thin).">
				>
				["at0015"] = <
					text = <"0 - 30%">
					description = <"Cervical effacement is estimated to 0 - 30%.">
				>
				["at0016"] = <
					text = <"40 - 50%">
					description = <"Cervical effacement is estimated to 40 - 50%.">
				>
				["at0017"] = <
					text = <"60 - 70%">
					description = <"Cervical effacement is estimated to 60 - 70%.">
				>
				["at0018"] = <
					text = <"80+ %">
					description = <"Cervical effacement is estimated to 80+ %.">
				>
				["at0019"] = <
					text = <"Cervical consistency">
					description = <"Softening of the cervix, a decline in the tissue tensile strength. ">
				>
				["at0020"] = <
					text = <"Firm">
					description = <"Cervical tissue tensile strength, cervix feels firm.">
				>
				["at0021"] = <
					text = <"Medium">
					description = <"Cervical tissue tensile strength, cervix feels neither firm nor soft, but in between.">
				>
				["at0022"] = <
					text = <"Soft">
					description = <"Cervicel tissue tensile strength, cervix feels soft.">
				>
				["at0023"] = <
					text = <"Cervical position">
					description = <"The position of the cervix changes and tends to become more anterior (nearer the opening of the vagina) as labour becomes closer. ">
				>
				["at0024"] = <
					text = <"Posterior">
					description = <"The cervix is located posteriorly, towards the sacrum.">
				>
				["at0025"] = <
					text = <"Middle">
					description = <"The cervix is located centrally, in the typical anatomical position.">
				>
				["at0026"] = <
					text = <"Anterior">
					description = <"The cervix is located anteriorly, towards the pubis.">
				>
				["at0027"] = <
					text = <"Total score">
					description = <"The total sum of all the Bishop score parameter ordinals.">
					comment = <"Total score may be generated at run-time. ">
				>
				["at0028"] = <
					text = <"ItemTree">
					description = <"@ internal @">
				>
				["at0030"] = <
					text = <"Comment">
					description = <"Additional narrative about the overall Bidhop score not captured in other fields. ">
				>
			>
		>
	>
